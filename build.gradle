import org.apache.tools.ant.filters.FixCrLfFilter
import org.apache.tools.ant.filters.ReplaceTokens

def filteredSourceDir = file("${buildDir}/filtered") 
def versionValue = '1.0'
def mainClass = "org.craft.bootstrap.BootstrapMain"

apply plugin: 'java'
sourceCompatibility = 1.6
archivesBaseName = 'OurCraftBootstrap'
version = versionValue

//IDE
apply plugin: 'eclipse'
apply plugin: 'idea'

repositories {
    mavenCentral()
}

//Tasks
task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

task processVersion(type: Copy) {
    from sourceSets.main.java 
    into filteredSourceDir 
    // Substitute property references in files
    // Use some of the filters provided by Ant
    filter(FixCrLfFilter)
    filter(ReplaceTokens, tokens: ['version': versionValue])
    // Use a closure to filter each line
    filter { String line ->
        ("$line".replaceAll('"OurCraft:BuildNumber"', '"'+versionValue+'"'))
    }
}

sourceSets { 
    // This source set will contain all sources that we filter 
    filtered { 
        java { 
            srcDirs = [filteredSourceDir] 
        } 
    } 
} 

//force UTF-8 to the compileJava task
compileJava.options.encoding = 'UTF-8'

// tell the compileJava task to compile the filtered source 
compileJava.source = sourceSets.filtered.java 

// Add dependencies processVersion to compileJava task
compileJava.dependsOn processVersion 

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    
}

jar {
    manifest {
        attributes 'Implementation-Title': 'OurCraftLauncher', 'Implementation-Version': version, 'Main-Class': mainClass
    }
}

task runClient(type: JavaExec, dependsOn: 'classes') {
    main = mainClass
    classpath = configurations.runtime
    classpath += sourceSets.main.runtimeClasspath
    standardInput = System.in
}